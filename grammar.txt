<Z> -> <program> #
<program> -> <program> <programNode> ; | <programNode> ;
<programNode> -> <declaration> | <statement>

<declaration> -> <variableDeclaration> | <constDeclaration> | <functionDeclaration>

<variableDeclaration> -> let <parameter>
<constDeclaration> -> const id <typeGuideOpt> = <expression>
<functionDeclaration> -> function id ( <paramList> ) <typeGuide> <blockStatement>
<paramList> -> <parameter> <paramListRem> | e
<paramListRem> -> , <parameter> <paramListRem> | e
<parameter> -> id <typeGuideOpt> = <expression> | id <typeGuide>
<typeGuideOpt> -> <typeGuide> | e
<typeGuide> -> : <type>

<statement> -> <blockStatement> | <whileStatement> | <returnStatement> | <ifStatement> | <expressionStatement>

<blockStatement> -> { <blockActions> }
<blockActions> -> <blockAction> ; <blockActions> | e
<blockAction> -> <declaration> | <statement>

<expressionStatement> -> <expression>

<whileStatement> -> while ( <expression> ) <blockStatement>

<returnStatement> -> return <expression>

<ifStatement> -> if ( <expression> ) <blockStatement> <else>
<else> -> else <elseStatement> | e
<elseStatement> -> <ifStatement> | <blockStatement>

<expression> -> <expression> <relOp> <simexp> | <simexp> | <arrowFunctionExpression>
<expListOpt> -> <expList> | e
<expList> -> <expList> , <expression> | <expression>
<simexp> -> <simexp> <lowPriorityOp> <simterm> | <simterm>
<simterm> -> <simterm> <highPriorityOp> <term> | <term>
<term> -> ( <expression> ) | + <term> | - <term> | not <term> | ! <term> | <ident> | int | float | true | false | string
<ident> -> <ident> [ <expression> ] | <ident> ( <expListOpt> ) | id
<relOp> -> < | > | <= | >= | == | !=
<lowPriorityOp> -> + | - | or
<highPriorityOp> -> * | mod | div | and

<arrowFunctionExpression> -> ( <paramList> ) -> <arrowFunctionNode>
<arrowFunctionNode> -> <expression> | <blockStatement>

<type> -> id | id -> <type>
